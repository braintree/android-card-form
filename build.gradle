buildscript {
    repositories {
        mavenCentral()
        google()
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'de.marcphilipp.gradle:nexus-publish-plugin:0.4.0'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2'
    }
}

plugins {
    id 'io.codearte.nexus-staging' version '0.21.2'
}

ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID') ?: ''
ext["signing.password"] = System.getenv('SIGNING_KEY_PASSWORD') ?: ''
ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_KEY_FILE') ?: ''

version '5.4.1-SNAPSHOT'
ext {
    compileSdkVersion = 35
    minSdkVersion = 21
    targetSdkVersion = 35
    versionCode = 52
    versionName = version
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

nexusStaging {
    packageGroup = "com.braintreepayments"
    // give nexus sonatype more time to close the staging repository
    delayBetweenRetriesInMillis = 20000
    username = System.env['SONATYPE_USERNAME']
    password = System.env['SONATYPE_PASSWORD']
}

task changeReleaseVersion {
    doLast {
        def gradleFile = new File('build.gradle')
        def gradleFileText = gradleFile.text.replaceFirst("\\nversion '\\d+\\.\\d+\\.\\d+(-.*)?'", "\nversion '" + versionParam + "'")
        gradleFile.write(gradleFileText)
    }
}

task changeREADMEVersion {
    doLast {
        def readmeFile = new File('README.md')
        def readmeFileText = readmeFile.text.replaceFirst("com.braintreepayments.api:card-form:\\d+\\.\\d+\\.\\d+(-.*)?'", "com.braintreepayments.api:card-form:" + versionParam + "'")
        readmeFile.write(readmeFileText)
    }
}

task incrementSNAPSHOTVersion {
    doLast {
        def gradleFile = new File('build.gradle')
        def (major, minor, patch) = versionParam.tokenize('.')
        def patchInteger = patch.toInteger()
        def newVersion = "$major.$minor.${patchInteger + 1}-SNAPSHOT"
        def gradleFileText = gradleFile.text.replaceFirst("\\nversion '\\d+\\.\\d+\\.\\d+(-.*)?'", "\nversion '" + newVersion + "'")
        gradleFile.write(gradleFileText)
    }
}

task incrementVersionCode {
    doLast {
        def gradleFile = new File('build.gradle')
        def versionText = gradleFile.text.find("versionCode = \\d+")
        def params = versionText.split("=")
        def newVersionCode = params[1].trim().toInteger() + 1
        def gradleFileText = gradleFile.text.replaceFirst("versionCode = \\d+", "versionCode = " + newVersionCode)
        gradleFile.write(gradleFileText)
    }
}