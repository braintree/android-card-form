apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 46
        versionName rootProject.ext.versionName
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            all {
                maxHeapSize = "1024m"
            }
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    resourcePrefix 'bt_'
}

dependencies {
    implementation 'com.google.android.material:material:1.0.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'org.mockito:mockito-core:2.8.9'
}

/* maven release */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.braintreepayments'
                artifactId = 'card-form'
                version = "${android.defaultConfig.versionName}"

                pom {
                    name = 'card-form'
                    packaging = 'aar'
                    description = 'A ready-made card form layout that can be included in your Android app, making it easy to accept credit and debit cards.'
                    url = 'https://github.com/braintree/android-card-form'

                    scm {
                        url = 'scm:git@github.com:braintree/android-card-form.git'
                        connection = 'scm:git@github.com:braintree/android-card-form.git'
                        developerConnection = 'scm:git@github.com:braintree/android-card-form.git'
                    }

                    developers {
                        developer {
                            id = 'devs'
                            name = 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}
