apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

version '4.0.0-SNAPSHOT'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 42
        versionName version

        consumerProguardFiles 'proguard.cfg'
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            all {
                jvmArgs '-noverify'
            }
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    resourcePrefix 'bt_'
}

dependencies {
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'io.card:android-sdk:5.5.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.mockito:mockito-core:2.8.9'
}

/* maven release */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

signing {
    required { !version.endsWith("SNAPSHOT") }
    sign configurations.archives
}

nexusStaging {
    packageGroup = "com.braintreepayments"
}

def sonatypeUsername = System.env['SONATYPE_USERNAME']
def sonatypePassword = System.env['SONATYPE_PASSWORD']

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.setArtifactId 'card-form'

            pom.project {
                name 'card-form'
                groupId = 'com.braintreepayments'
                version = "${android.defaultConfig.versionName}"
                packaging 'aar'
                description 'A ready-made card form layout that can be included in your Android app, making it easy to accept credit and debit cards.'

                url 'https://github.com/braintree/android-card-form'

                scm {
                    url 'scm:git@github.com:braintree/android-card-form.git'
                    connection 'scm:git@github.com:braintree/android-card-form.git'
                    developerConnection 'scm:git@github.com:braintree/android-card-form.git'
                }

                developers {
                    developer {
                        id 'devs'
                        name 'Braintree Payments'
                    }
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
            }

            pom.whenConfigured { MavenPom pom ->
                pom.dependencies.each {
                    if (it.artifactId == 'android-sdk') {
                        it.optional = true
                    }
                }
            }
        }
    }
}
