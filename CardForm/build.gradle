plugins {
    id 'com.android.library'
    id 'de.marcphilipp.nexus-publish'
    id 'signing'
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }


    resourcePrefix 'bt_'
    namespace 'com.braintreepayments.api.cardform'
    lint {
        textOutput file('stdout')
        textReport true
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'androidx.test:core:1.4.0'
}

// region signing and publishing

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

signing {
    required {
        !version.endsWith("SNAPSHOT") && !version.endsWith("DEVELOPMENT")
    }
    sign publishing.publications
}

nexusPublishing {
    // give nexus sonatype more time to initialize the staging repository
    clientTimeout = Duration.ofMinutes(3)
    repositories {
        sonatype()
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.braintreepayments'
                artifactId = 'card-form'
                version = rootProject.versionName

                pom {
                    name = 'card-form'
                    packaging = 'aar'
                    description = 'A ready-made card form layout that can be included in your Android app, making it easy to accept credit and debit cards.'
                    url = 'https://github.com/braintree/android-card-form'

                    scm {
                        url = 'scm:git@github.com:braintree/android-card-form.git'
                        connection = 'scm:git@github.com:braintree/android-card-form.git'
                        developerConnection = 'scm:git@github.com:braintree/android-card-form.git'
                    }

                    developers {
                        developer {
                            id = 'devs'
                            name = 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

// endregion
