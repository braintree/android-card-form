plugins {
    id 'com.android.library'
}

android {
    compileSdk rootProject.compileSdkVersion
    namespace "com.braintreepayments.cardform"

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    resourcePrefix 'bt_'
}

dependencies {
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'androidx.test:core:1.4.0'
}

// region signing and publishing

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

project.ext.name = "card-form"
project.ext.pom_name = "card-form"
project.ext.group_id = "com.braintreepayments"
project.ext.version = rootProject.version
project.ext.pom_desc = "A ready-made card form layout that can be included in your Android app, making it easy to accept credit and debit cards."

apply from: rootProject.file("gradle/gradle-publish.gradle")

// endregion
